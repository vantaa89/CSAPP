#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
#
# Describe how and why you modified the baseline code.
#
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
	xorq %rax,%rax		# count = 0;
	andq %rdx, %rdx
	jle Done
	rrmovq %rdx, %r14
	iaddq $-4, %r14
	jle Loop1
	
Loop2: 
	mrmovq (%rdi), %r8	# read val from src...
	mrmovq 8(%rdi), %r9	# read val from src...
	mrmovq 16(%rdi), %r10	# read val from src...
	mrmovq 24(%rdi), %r11	# read val from src...
	rmmovq %r8, (%rsi)	# ...and store it to dst
	rmmovq %r9, 8(%rsi)	# ...and store it to dst
	rmmovq %r10, 16(%rsi)	# ...and store it to dst
	rmmovq %r11, 24(%rsi)	# ...and store it to dst
	andq %r8, %r8		# val <= 0?
	jle Npos21		# if so, goto Npos:
	iaddq $1, %rax		# count++
Npos21:	
	andq %r9, %r9		# val <= 0?
	jle Npos22		# if so, goto Npos:
	iaddq $1, %rax		# count++
Npos22:	
	andq %r10, %r10		# val <= 0?
	jle Npos23		# if so, goto Npos:
	iaddq $1, %rax		# count++
Npos23:	
	andq %r11, %r11		# val <= 0?
	jle Npos24		# if so, goto Npos:
	iaddq $1, %rax		# count++
Npos24:
	iaddq $-4, %rdx
	iaddq $32, %rdi
	iaddq $32, %rsi
	rrmovq %rdx, %r10
	iaddq $-4, %r10
	jg Loop2

Loop1:	mrmovq (%rdi), %r10	# read val from src...
	rmmovq %r10, (%rsi)	# ...and store it to dst
	andq %r10, %r10		# val <= 0?
	jle Npos1		# if so, goto Npos:
	iaddq $1, %rax		# count++
Npos1:	
	iaddq $-1, %rdx		# len--
	iaddq $8, %rdi		# src++
	iaddq $8, %rsi		# dst++
	andq %rdx,%rdx		# len > 0?
	jg Loop1			# if so, goto Loop:
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
